plugins {
    id("com.android.library")
    id("kotlin-android")
    id("maven-publish")
    id("org.jetbrains.kotlinx.binary-compatibility-validator")  version "0.8.0"
}

android {
    compileSdkVersion(33)

    defaultConfig {
        minSdkVersion(21)
        targetSdkVersion(33)
        setProperty("archivesBaseName", "com.transferwise.sequencelayout-${versionName}")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.0-alpha02"
    }
}

ext {
    PUBLISH_GROUP_ID = "com.github.transferwise"
    PUBLISH_ARTIFACT_ID = "sequence-layout"
    PUBLISH_VERSION = "1.3.0"
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = PUBLISH_GROUP_ID
                artifactId = PUBLISH_ARTIFACT_ID
                version = PUBLISH_VERSION

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    url = "https://github.com/transferwise/sequence-layout"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    scm {
                        connection = "scm:git:github.com/transferwise/sequence-layout.git"
                        developerConnection = "scm:git:ssh://github.com/transferwise/sequence-layout.git"
                        url = "https://github.com/transferwise/sequence-layout/tree/master"
                    }
                }
            }
        }
    }
}

dependencies {
    implementation platform('androidx.compose:compose-bom:2023.01.00')

    implementation("androidx.compose.foundation:foundation")
    implementation("androidx.compose.material:material")
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.ui:ui-tooling")

    implementation("androidx.appcompat:appcompat:1.6.0")
    implementation("androidx.core:core-ktx:1.9.0")
}
